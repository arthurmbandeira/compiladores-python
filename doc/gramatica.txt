<program> => <sequenciaDeclaracao>

<declaracao> => <declaracaoVariavel>
              | id '(' <listaParametros> ')' '{' <bloco> '}'
              | <tipo> id '(' <listaParametros> ')' '{' <bloco> '}'

<declaracaoVariavel> => <tipo> <sequenciaSpecVariavel> ';'

<especificacaoVariavel> => id
                         | id '=' <literal>
                         | id '[' numero ']'
                         | id '[' numero ']' '=' '{' <sequenciaLiteral> '}'

<tipo> => "int"
        | "string"
        | "bool"

<parametros> => <tipo> id
              | <tipo> id '[' ']'

<bloco> => <listaDeclaracaoVariavel> <listaComandos>

<comando> => <comandoIF>
           | <comandoWhile>
           | <comandoFor>
           | <comandoBreak>
           | <comandoReturn>
           | <comandoRead>
           | <comandoWrite>
           | <atribuicao> ';'
           | <chamada> ';'

<comandoIf> => "if" '(' <expressao> ')' '{' <bloco> '}'
             | "if" '(' <expressao> ')' '{' <bloco> '}' "else" '{' <bloco> '}'

<comandoWhile> => "while" '(' <expressao> ')' '{' <bloco> '}'

<comandoFor> => "for" '(' <atribuicao> ';' <expressao> ';' <atribuicao> ')' '{' <bloco> '}'

<comandoBreak> => "break" ';'

<comandoRead> => "read" <variavel> ';'

<comandoWrite> => "write" <listaExpressao> ';'

<comandoReturn> => "return" ';'
                 | "return" <expressao> ';'

<chamada> => id '(' <listaExpressao> ')'

<atribuicao> => <variavel> '='  <expressao>
              | <variavel> "+=" <expressao>
              | <variavel> "-=" <expressao>
              | <variavel> "*=" <expressao>
              | <variavel> "/=" <expressao>
              | <variavel> "%=" <expressao>

<variavel> => id
            | id '[' <expressao> ']'

<expressao> => <expressao> '+'   <expressao>
             | <expressao> '-'   <expressao>
             | <expressao> '*'   <expressao>
             | <expressao> '/'   <expressao>
             | <expressao> '%'   <expressao>
             | <expressao> "=="  <expressao>
             | <expressao> "!="  <expressao>
             | <expressao> "<="  <expressao>
             | <expressao> ">="  <expressao>
             | <expressao> '>'   <expressao>
             | <expressao> '<'   <expressao>
             | <expressao> "&&"  <expressao>
             | <expressao> "||"  <expressao>
             | '!' <expressao>
             | '-' <expressao>
             | <expressao> '?' <expressao> ':' <expressao>
             | <chamada>
             | <variavel>
             | <literal>
             | '(' <expressao> ')'

<literal> => numero
           | string
           | logico

<listaParametros> => <sequenciaParametros>
                   | ε

<sequenciaParametros> => <parametros> ',' <sequenciaParametros>
                       | <parametros>

<listaDeclaracaoVariavel> => <declaracaoVariavel> <listaDeclaracaoVariavel>
                           | ε

<sequenciaSpecVariavel> => <especificacaoVariavel> ',' <sequenciaSpecVariavel>
                         | <especificacaoVariavel>

<sequenciaDeclaracao> => <declaracao> <sequenciaDeclaracao>
                       | <declaracao>

<listaComandos> => <comando> <listaComandos>
                 | ε

<sequenciaLiteral> => <literal> <sequenciaLiteral>
                    | <literal>

<listaExpressao> => <sequenciaExpressao>
                  | ε

<sequenciaExpressao> => <expressao> ',' <sequenciaExpressao>
                      | <expressao>
